# Copyright (C) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#	http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
import("//build/test.gni")
import("../../../../test_template.gni")
import("../adlt_common.gni")
import("test_src_adlt_hwasan.gni")

group("adlt_hwasan_test") {
    testonly = true
    deps = []

    foreach(s, adlt_hwasan_test) {
        deps += [ ":${s}" ]
    }
}

config("post_lib_configs") {
    cflags_cc = [ "-fno-lto", "-fsanitize=hwaddress", "-fno-emulated-tls" ]
    ldflags = cflags_cc + [ "-Wl,--unresolved-symbols=ignore-all", "-Wl,-T" + rebase_path("build-id/linker_script.ld", ""), "-Wl,--build-id" ]
}

adlt_executable("unittest_adlt_hwasan_build_id") {
    lib_name = "hwasan_build_id"
    lib_include_dirs = [ "build-id" ]
    lib_configs = [
        {
            name = "hwasan_build_id_a"
            sources = [ "build-id/a.cpp" ]
        },
        {
            name = "hwasan_build_id_b"
            sources = [ "build-id/b.cpp" ]
        }
    ]

    lib_common_configs = [
        ":post_lib_configs"
    ]

    include_dirs = [ "build-id" ]
    sources = [ "build-id/main.cpp" ]
    ldflags = [ "-Wl,$adlt_abs_runtime_clang_path/libclang_rt.hwasan.a" ]
}

action("post_unittest_adlt_hwasan_build_id") {
    testonly = true
    deps = [ ":unittest_adlt_hwasan_build_id" ]
    script = "../adlt_tools.py"
    args = [
        "get_build_id",
        "--tool-path", rebase_path(adlt_llvm_readelf, ""),
        "--output-file", rebase_path(adlt_lib_dir, "") + "/unittest_adlt_hwasan_build_id_output.txt",
        "--file-list",
        rebase_path(adlt_moved_lib_dir, "") + "/libadlt_hwasan_build_id_a.so",
        rebase_path(adlt_moved_lib_dir, "") + "/libadlt_hwasan_build_id_b.so",
    ]
    outputs = [ "$adlt_bin_dir/unittest_adlt_hwasan_build_id_output.txt" ]
}